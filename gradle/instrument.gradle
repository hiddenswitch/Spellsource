// See ../quasar-core/README.md for an explanation of this file
apply plugin: 'java-library'

configurations {
    uninstrumentedClasspath {
        canBeConsumed = false
        canBeResolved = true
        extendsFrom implementation
    }

    uninstrumentedJars {
        canBeConsumed = true
        canBeResolved = false
    }
}

dependencies {
    implementation(project(path: ':quasar-core', configuration: 'shadowedJar')) {
        exclude group: 'org.ow2.asm', module: '*'
        because "it has been relocated in the quasar shadow jar"
    }
}

task uninstrumentedCompileJava(type: JavaCompile) {
    exclude '**/module-info.java'
    source(sourceSets.main.java)
    classpath = configurations.uninstrumentedClasspath.asFileTree
    destinationDir = file(project.buildDir.path + '/uninstrumentedClasses/java/main')
}

task uninstrumentedJar(type: org.gradle.jvm.tasks.Jar) {
    dependsOn uninstrumentedCompileJava, processResources
    from { uninstrumentedCompileJava.destinationDir }
    from { processResources.destinationDir }
    archiveClassifier = 'uninstrumented'
}

artifacts {
    uninstrumentedJars(uninstrumentedJar.archiveFile) {
        builtBy(uninstrumentedJar)
    }
}

compileJava.dependsOn processResources
compileJava {
    doLast {
        def classpath = configurations.compileClasspath.asPath + ':' + configurations.runtimeClasspath.asPath

        ant.taskdef(name: 'scanSuspendables', classname: 'co.paralleluniverse.fibers.instrument.SuspendablesScanner', classpath: classpath)
        ant.scanSuspendables(
                auto: true,
                supersFile: "$sourceSets.main.resources.dirs.asPath/META-INF/suspendable-supers",
                append: false) {
            fileset(dir: sourceSets.main.output.classesDirs.asPath)
        }
        ant.taskdef(name: 'instrumentation', classname: 'co.paralleluniverse.fibers.instrument.InstrumentationTask',
                classpath: classpath)
        ant.instrumentation(allowMonitors: 'true', allowBlocking: 'true') {
            fileset(dir: sourceSets.main.output.classesDirs.asPath)
        }
    }
}

compileTestJava.dependsOn processTestResources
compileTestJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

    doLast {
        ant.taskdef(name: 'instrumentation', classname: 'co.paralleluniverse.fibers.instrument.InstrumentationTask',
                classpath: configurations.compileClasspath.asPath + ':' + configurations.runtimeClasspath.asPath)
        ant.instrumentation(allowMonitors: 'true', allowBlocking: 'true', check: 'true', debug: 'true') {
            fileset(dir: sourceSets.main.output.classesDirs.asPath)
            fileset(dir: sourceSets.test.output.classesDirs.asPath)
        }
    }
}
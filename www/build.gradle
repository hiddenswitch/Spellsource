plugins {
    id "com.github.node-gradle.node" version "2.2.4"
    id 'com.github.johnrengelman.shadow' version "$shadowPluginVersion"
    id 'java-library'
}

apply plugin: "com.github.node-gradle.node"
apply plugin: 'java-library'
apply plugin: 'maven'

dependencies {
    api project(':spellsource-common')
    api project(':spellsource-game')
    api project(':spellsource-cards-git')
    api project(':spellsource-testutils')
    runtimeOnly group: 'org.codehaus.groovy', name: 'groovy', version: '3.0.2'
}

test {
    dependsOn += [shadowJar]
    useJUnitPlatform()
}

shadowJar {
    mergeServiceFiles()
    zip64 true
}

node {
    download = false
}

task run(type: NpxTask) {
    dependsOn += [npmInstall]
    command = 'gatsby'
    args = ['develop', '-o']
}

task copyGameJavadoc(type: Copy) {
    description("Copies the game module's javadoc into the public directory of the website")
    dependsOn ':spellsource-game:javadoc'
    from tasks.getByPath(':spellsource-game:javadoc').outputs
    into './www/public/javadoc'
}

task copyNetJavadoc(type: Copy) {
    description("Copies the net module's javadoc into the public directory of the website")
    dependsOn ':spellsource-server:javadoc'
    from tasks.getByPath(':spellsource-server:javadoc').outputs
    into './www/public/netjavadoc'
}

// todo: depends on .venv
task buildWWW(type: ExecSourceTask) {
    description("Builds the website")
    source 'src'
    exclude Utilities.gitIgnore(file("www/.gitignore"))
    outputs.dir('./src/public')
    commandLine './build.sh'
}

task distWWW(type: Exec, group: 'Spellsource') {
    description("Builds and deploys the website (requires npm, python, the .venv virtualenv installed)")
    dependsOn += [buildWWW, copyGameJavadoc, copyNetJavadoc]
    commandLine './deploy.sh'
}
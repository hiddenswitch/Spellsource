apply plugin: 'base'

def spellsourceDepTask = project(':spellsource-web-cardeditor-support').tasks.shadowJar

task npmInstallGraal {
    inputs.file('package.json')
    inputs.file('package-lock.json')
    outputs.dir('./node_modules')

    def javaHome = System.properties['java.home']
    doLast {
        exec {
            commandLine 'npm', 'install', '--legacy-peer-deps', "--nodedir=$javaHome/languages/js", '--build-from-source'
        }
    }
}

task test {
    dependsOn += [spellsourceDepTask, npmInstallGraal]
    doLast {
        exec {
            commandLine 'node', '--polyglot', '--jvm', './node_modules/jest/bin/jest.js', '--colors', '--verbose'
        }
    }
}

task copyGameJavadoc(type: Copy) {
    description("Copies the game module's javadoc into the public directory of the website")
    dependsOn ':spellsource-game:javadoc'
    from tasks.getByPath(':spellsource-game:javadoc').outputs
    into './www/public/javadoc'
}

task copyNetJavadoc(type: Copy) {
    description("Copies the net module's javadoc into the public directory of the website")
    dependsOn ':spellsource-server:javadoc'
    from tasks.getByPath(':spellsource-server:javadoc').outputs
    into './www/public/netjavadoc'
}

// todo: depends on .venv
task buildWWW(type: ExecSourceTask) {
    description("Builds the website")
    source 'src'
    exclude Utilities.gitIgnore(file("www/.gitignore"))
    outputs.dir('./src/public')
    commandLine './build.sh'
}

task distWWW(type: Exec, group: 'Spellsource') {
    description("Builds and deploys the website (requires npm, python, the .venv virtualenv installed)")
    dependsOn += [buildWWW, copyGameJavadoc, copyNetJavadoc]
    commandLine './deploy.sh'
}
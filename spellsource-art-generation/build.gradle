plugins {
//  id 'docker'
  id "org.openapi.generator" version "7.0.0-beta"
}


import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

// Top-level declarations
def venvDir = 'venv'
def isWindows = System.properties['os.name'].toLowerCase().contains('windows')
def scriptsDir = isWindows ? "${venvDir}/Scripts" : "${venvDir}/bin"
def sitePackagesDir = isWindows ?
        "${venvDir}/Lib/site-packages" :
        // todo: get the path for real using
        // python -c "import site; print(site.getsitepackages()[0])"
        "${venvDir}/lib/python3.11/site-packages"
def fileExt = isWindows ? ".exe" : ""
tasks.register('venv') {
  // Specify the outputs of the task
  outputs.file "venv/pyvenv.cfg"
  outputs.cacheIf { true }
  outputs.upToDateWhen { file(venvDir).exists() }
  onlyIf { !file(venvDir).exists() }

  doLast {
    // Create the virtual environment
    exec {
      workingDir(project.layout.projectDirectory)
      commandLine 'python', '-m', 'venv', venvDir
    }
  }
}

[["comfyui", "comfyui"], ["spellsource_art_generation", "spellsource_art_generation"]].each { tuple ->
  def dirName = tuple[0]
  def packageName = tuple[1]
  tasks.register("${packageName}_installEditable") {
    dependsOn 'venv'
    inputs.files "$dirName/requirements.txt", "$dirName/setup.py"
    outputs.cacheIf { false }

    def editablePath = "${sitePackagesDir}/__editable__.${packageName}-0.0.1.pth"
    outputs.file editablePath

    doLast {
      exec {
        workingDir(layout.projectDirectory.dir(dirName))
        commandLine layout.projectDirectory.file("${scriptsDir}/pip${fileExt}").asFile.path, 'install', "-e", ".", "--config-settings", "editable_mode=compat"
      }
    }
  }
}

tasks.register('install') {
  dependsOn "comfyui_installEditable", "spellsource_art_generation_installEditable"
}

tasks.register("downloadModels") {
  def srcUrl = 'https://huggingface.co/stabilityai/stable-diffusion-xl-base-1.0/resolve/main/sd_xl_base_1.0.safetensors'
  def destPath = "$projectDir/workdir/models/checkpoints/sd_xl_base_1.0.safetensors"

  inputs.property("srcUrl", srcUrl)
  outputs.file(destPath)
  outputs.upToDateWhen { file(destPath).exists() }
  onlyIf { !file(destPath).exists() }
  outputs.cacheIf { false }

  doLast {
    exec {
      commandLine(["curl", "-sL", srcUrl, "-o", destPath])
    }
  }
}

tasks.register('run') {
  group('spellsource')
  dependsOn 'install', 'downloadModels'
  doLast {
    // Run the comfyui binary from the virtual environment
    exec {
      workingDir(layout.projectDirectory.dir("workdir"))
      commandLine([layout.projectDirectory.file("${scriptsDir}/comfyui${fileExt}").asFile.path, "--listen", "--enable-cors-header"])
    }
  }
}

tasks.register("generateTypeScriptClient", GenerateTask) {
  group('spellsource')
  def inputSpecFile = provider { file("comfyui/comfy/api/openapi.yaml") }
  // don't even configure if it doesn't exist
  onlyIf { inputSpecFile.map { it.exists() } }
  inputSpec.set(inputSpecFile.map { it.exists() ? it.path : file("dummy.yaml").path })
  outputDir.set(layout.buildDirectory.dir("comfyclient").map { it.asFile.path })
  validateSpec.set(inputSpecFile.map { it.exists() })
  generatorName = 'typescript'
  outputs.cacheIf { true }
  configOptions.putAll([
          'fileContentDataType'   : 'Buffer', // this is apparently substituted in verbatim and should be 'Blob' for browsers
          'enumUnknownDefaultCase': 'true',
          'platform'              : 'browser', // or 'browser' or 'deno'
          'supportsES6'           : 'true',
          'useObjectParameters'   : 'true',
          'framework'             : 'fetch-api',
          'modelPropertyNaming'   : 'original'
  ])
}
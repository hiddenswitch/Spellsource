apply from: 'client.gradle'

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    sourceCompatibility = 1.8
    project.version = '0.8.0'
    
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        compile group: 'org.jetbrains', name: 'annotations-java5', version: '15.0'
        compile group: 'de.ruedigermoeller', name: 'fst', version: '2.56'
        compile group: 'co.paralleluniverse', name: 'quasar-core', version: '0.8.0'
        runtime group: 'org.codehaus.groovy', name: 'groovy', version: '2.5.+'
    }

    compileJava.dependsOn processResources
    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

        doLast {
            ant.taskdef(name: 'instrumentation', classname: 'co.paralleluniverse.fibers.instrument.InstrumentationTask', classpath: configurations.compile.asPath)
            ant.instrumentation(allowMonitors: 'true', allowBlocking: 'true') {
                fileset(dir: sourceSets.main.output.classesDirs.asPath)
            }
        }
    }

    compileTestJava.dependsOn processTestResources
    compileTestJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

        doLast {
            ant.taskdef(name: 'instrumentation', classname: 'co.paralleluniverse.fibers.instrument.InstrumentationTask', classpath: configurations.testCompile.asPath)
            ant.instrumentation(allowMonitors: 'true', allowBlocking: 'true') {
                fileset(dir: sourceSets.main.output.classesDirs.asPath)
                fileset(dir: sourceSets.test.output.classesDirs.asPath)
            }
        }
    }
}

if (System.env.TRAVIS == 'true') {
    allprojects {
        tasks.withType(GroovyCompile) {
            groovyOptions.fork = false
        }
        tasks.withType(Test) {
            // containers (currently) have 2 dedicated cores and 4GB of memory
            maxParallelForks = 2
            minHeapSize = '128m'
        }
    }
}
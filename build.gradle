buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath('io.swagger:swagger-codegen:2.3.0')
    }
}

import io.swagger.codegen.config.CodegenConfigurator
import io.swagger.codegen.DefaultGenerator

task(swagger) {
    def input = "swagger-api.yaml"
    inputs.file(input)
    def output = file('client')
    outputs.dir(output)
    doLast {
        def config = new CodegenConfigurator()
        config.setInputSpec(input)
        config.setOutputDir(output.path)
        config.setTemplateDir('swagger-templates/java')
        config.setLang('java')
        config.setAdditionalProperties([
                'invokerPackage'         : 'com.hiddenswitch.spellsource.client',
                'modelPackage'           : 'com.hiddenswitch.spellsource.client.models',
                'apiPackage'             : 'com.hiddenswitch.spellsource.client.api',
                'library'                : 'jersey1',
                'serializableModel'      : 'true',
                'dateLibrary'            : 'java8',
                'artifactId'             : 'client',
                'hideGenerationTimestamp': 'true'
        ])
        config.setGroupId('com.hiddenswitch')
        config.setArtifactId('spellsource-client')
        new DefaultGenerator().opts(config.toClientOptInput()).generate()
    }
}


task(swaggerClient) {
    def input = "swagger-api.yaml"
    def outputDir = 'clientcsharp'
    def output = file(outputDir)
    inputs.file(input)
    outputs.dir(output)
    doLast {
        def config = new CodegenConfigurator()
        config.setInputSpec(input)
        config.setOutputDir(output.path)
        config.setLang('csharp')
        config.setAdditionalProperties([
                'supportFiles'           : 'false',
                'apiTests'               : 'false',
                'modelTests'             : 'false',
                'modelDocs'              : 'false',
                'packageName'            : 'Spellsource.Client',
                'targetFramework'        : 'v3.5',
                'sourceFolder'           : 'Scripts',
                'hideGenerationTimestamp': 'true',
                'optionalProjectFile'    : 'false',
                'optionalAssemblyFile'   : 'false'
        ])
        new DefaultGenerator().opts(config.toClientOptInput()).generate()
    }
}

subprojects {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    sourceCompatibility = 1.8

    project.version = '1.3.0'

    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    dependencies {
        compile 'org.jetbrains:annotations-java5:15.0'
        compile 'de.ruedigermoeller:fst:2.56'
        runtime 'org.codehaus.groovy:groovy:2.3.0'
    }
}

if (System.env.TRAVIS == 'true') {
    allprojects {
        tasks.withType(GroovyCompile) {
            groovyOptions.fork = false
        }
        tasks.withType(Test) {
            // containers (currently) have 2 dedicated cores and 4GB of memory
            maxParallelForks = 2
            minHeapSize = '128m'
        }
    }
}
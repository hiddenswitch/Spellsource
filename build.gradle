buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'gradle.plugin.org.detoeuf:swagger-codegen-plugin:1.6.3'
    }
}

apply plugin: 'org.detoeuf.swagger-codegen'

swagger {
    inputSpec = 'net/src/main/resources/server.yaml'

    output = 'client'
    language = 'java'

    additionalProperties = [
            'invokerPackage'         : 'com.hiddenswitch.spellsource.client',
            'modelPackage'           : 'com.hiddenswitch.spellsource.client.models',
            'apiPackage'             : 'com.hiddenswitch.spellsource.client.api',
            'library'                : 'okhttp-gson',
            'serializableModel'      : 'true',
            'dateLibrary'            : 'java8',
            'artifactId'             : 'client',
            'hideGenerationTimestamp': 'true'
    ]
}

subprojects {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    sourceCompatibility = 1.8

    project.version = '1.3.0'

    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

if (System.env.TRAVIS == 'true') {
    allprojects {
        tasks.withType(GroovyCompile) {
            groovyOptions.fork = false
        }
        tasks.withType(Test) {
            // containers (currently) have 2 dedicated cores and 4GB of memory
            maxParallelForks = 2
            minHeapSize = '128m'
        }
    }
}
buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'io.swagger:swagger-codegen:2.3.0'
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.8.0'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.20.0"
    }
}

apply plugin: 'java'


import io.swagger.codegen.ClientOptInput
import io.swagger.codegen.ClientOpts
import io.swagger.codegen.CodegenProperty
import io.swagger.codegen.DefaultGenerator
import io.swagger.codegen.config.CodegenConfigurator
import io.swagger.codegen.languages.CSharpClientCodegen
import io.swagger.models.Swagger
import io.swagger.parser.SwaggerParser

task(swagger) {
    def input = "swagger-api.yaml"
    def output = file('client')
    def templateDir = file('swagger-templates/java')

    inputs.file(input)
    outputs.dir(output)
    doLast {
        def config = new CodegenConfigurator()
        config.setInputSpec(input)
        config.setOutputDir(output.path)
        config.setTemplateDir(templateDir.absolutePath)
        config.setLang('java')
        config.setAdditionalProperties([
                'invokerPackage'         : 'com.hiddenswitch.spellsource.client',
                'modelPackage'           : 'com.hiddenswitch.spellsource.client.models',
                'apiPackage'             : 'com.hiddenswitch.spellsource.client.api',
                'library'                : 'jersey1',
                'serializableModel'      : 'true',
                'dateLibrary'            : 'java8',
                'artifactId'             : 'client',
                'artifactVersion'        : '0.8.7',
                'hideGenerationTimestamp': 'true'
        ])
        config.setGroupId('com.hiddenswitch')
        config.setArtifactId('spellsource-client')
        new DefaultGenerator().opts(config.toClientOptInput()).generate()
    }
}


class CSharpNullableGenerator extends CSharpClientCodegen {
    @Override
    String getTypeDeclaration(io.swagger.models.properties.Property p) {
        def declaration = super.getTypeDeclaration(p)
        if (!Boolean.parseBoolean(p.vendorExtensions.getOrDefault("x-nullable", "true").toString()) && declaration.contains("?")) {
            return declaration.replaceAll("\\?", "")
        }
        return declaration
    }

    @Override
    void updateCodegenPropertyEnum(CodegenProperty var) {
        super.updateCodegenPropertyEnum(var)
        if (var.vendorExtensions.containsKey("x-enum-varnames")) {
            List<Map<String, String>> enumVars = (List<Map<String, String>>) var.allowableValues.get("enumVars");
            def varNames = (ArrayList) var.vendorExtensions.get("x-enum-varnames");
            for (int i = 0; i < enumVars.size(); i++) {
                enumVars.get(i).put("xEnumVarname", (String) varNames.get(i));
            }
        }
    }
}

task(swaggerClient) {
    def input = "swagger-api.yaml"
    def outputDir = 'clientcsharp'
    def output = file(outputDir)
    def templateDir = file('swagger-templates/csharp')
    inputs.file(input)
    outputs.dir(output)
    doLast {
        def config = new CSharpNullableGenerator()
        config.setInputSpec(input)
        config.setOutputDir(output.path)
        config.setTemplateDir(templateDir.absolutePath)
        config.additionalProperties().putAll([
                'supportFiles'           : 'false',
                'apiTests'               : 'false',
                'modelTests'             : 'false',
                'modelDocs'              : 'false',
                'packageName'            : 'Spellsource.Client',
                'targetFramework'        : 'v3.5',
                'sourceFolder'           : 'Scripts',
                'hideGenerationTimestamp': 'true',
                'optionalProjectFile'    : 'false',
                'optionalAssemblyFile'   : 'false'
        ])

        def optInput = new ClientOptInput();
        Swagger swagger = new SwaggerParser().read(input, [], true);
        optInput.config(config)
                .opts(new ClientOpts())
                .swagger(swagger)
        new DefaultGenerator().opts(optInput).generate()
    }
}

apply plugin: "com.vanniktech.maven.publish"
apply plugin: 'io.codearte.nexus-staging'

mavenPublish {
    targets {
        uploadArchives {
            releaseRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            snapshotRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
}


allprojects {
    java {
        // We are always source-compatible with Java 1.8 (no advanced language features)
        sourceCompatibility = JavaVersion.VERSION_1_8
        // But we target the JVM that is currently used to compile this code. Many Windows users can only practically
        // install Java 8 because that's what there are installers for, so we have a parallel support for it. To build
        // for Java 8, the JAVA_HOME must point to a Java 8 JDK and gradle should be started with 
        targetCompatibility = JavaVersion.current()
    }

    // Standardize versions of all the dependent modules in our projects
    configurations.all {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                if (['com.fasterxml.jackson.core', 'com.fasterxml.jackson.jaxrs'].contains(details.requested.group)) {
                    details.useVersion "2.9.9"
                }

                if (details.requested.module.name == 'netty-all') {
                    details.useVersion "4.1.34.Final"
                }

                if (details.requested.module.name.contains('jackson-databind')) {
                    details.useVersion "2.9.9.1"
                }

                if (details.requested.group == 'org.apache.commons' && details.requested.module.name == "commons-math3") {
                    details.useVersion "3.5"
                }

                if (details.requested.group == 'commons-lang') {
                    details.useVersion "2.5"
                }

                if (details.requested.group == 'commons-collections') {
                    details.useVersion "3.2.2"
                }

                if (details.requested.group == 'commons-io') {
                    details.useVersion "2.5"
                }

                if (details.requested.group == 'commons-logging') {
                    details.useVersion "1.2"
                }

                if (details.requested.group == 'ch.qos.logback') {
                    details.useVersion "1.1.7"
                }
            }
        }
    }

    project.version = '0.8.54'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: "com.vanniktech.maven.publish"

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    compileJava.dependsOn processResources
    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

        if (project.name != 'quasar-core' && project.name != 'client') {
            doLast {
                ant.taskdef(name: 'instrumentation', classname: 'co.paralleluniverse.fibers.instrument.InstrumentationTask', classpath: configurations.compile.asPath)
                ant.instrumentation(allowMonitors: 'true', allowBlocking: 'true') {
                    fileset(dir: sourceSets.main.output.classesDirs.asPath)
                }
            }
        }
    }

    compileTestJava.dependsOn processTestResources
    compileTestJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

        doLast {
            if (project.name != 'quasar-core' && project.name != 'client') {
                ant.taskdef(name: 'instrumentation', classname: 'co.paralleluniverse.fibers.instrument.InstrumentationTask', classpath: configurations.testCompile.asPath)
                ant.instrumentation(allowMonitors: 'true', allowBlocking: 'true') {
                    fileset(dir: sourceSets.main.output.classesDirs.asPath)
                    fileset(dir: sourceSets.test.output.classesDirs.asPath)
                }
            }
        }
    }
}

if (System.env.TRAVIS == 'true') {
    allprojects {
        tasks.withType(GroovyCompile) {
            groovyOptions.fork = false
        }
        tasks.withType(Test) {
            // containers (currently) have 2 dedicated cores and 4GB of memory
            maxParallelForks = 2
            minHeapSize = '128m'
        }
    }
}

import java.nio.file.FileVisitResult
import java.nio.file.Files
import java.nio.file.attribute.BasicFileAttributes

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.hiddenswitch'
            artifactId = 'game'
            version = project.version

            from components.java
        }
    }
}

dependencies {
    compile project(':client')
    compile project(':vertx-sync')

    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.3'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    compile group: 'org.apache.commons', name: 'commons-math3', version: '3.5'
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'io.vertx', name: 'vertx-core', version: '3.6.0'

    testCompile project(':cards')

    testCompile group: 'org.jsoup', name: 'jsoup', version: '1.11.2'
    testCompile group: 'org.testng', name: 'testng', version: '6.+'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.+'
    testCompile group: 'org.unitils', name: 'unitils-core', version: '3.4.3'
    testCompile group: 'org.reflections', name: 'reflections', version: '0.9.11'
}

test {
    // enable TestNG support (default is JUnit)
    useTestNG()
    enableAssertions = false
    testLogging {
        events "standardError"
    }
}

task scanSuspendables {
    doLast {
        ant.taskdef(name: 'scanSuspendables', classname: 'co.paralleluniverse.fibers.instrument.SuspendablesScanner',
                classpath: "build/classes/main:build/resources/main:${configurations.runtime.asPath}")
        ant.scanSuspendables(
                auto: true,
                suspendablesFile: "$sourceSets.main.output.resourcesDir/META-INF/suspendables",
                supersFile: "$sourceSets.main.output.resourcesDir/META-INF/suspendable-supers") {
            fileset(dir: sourceSets.main.output.classesDir)
        }
    }
}
plugins {
    id 'com.github.johnrengelman.shadow' version "5.2.0"
    id 'org.javamodularity.moduleplugin' version '1.6.0'
    id 'application'
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'application'
apply plugin: 'org.javamodularity.moduleplugin'
apply from: '../gradle/instrument.gradle'

def localMainClassName = 'com.hiddenswitch.spellsource.applications.LocalClustered'

def debugSystemProperties = ["spellsource.port"                        : System.getenv().getOrDefault("PORT", "8080"),
                             "spellsource.root.logger.level"           : "DEBUG",
                             "games.defaultNoActivityTimeout"          : "999999000",
                             "vertx.logger-delegate-factory-class-name": "io.vertx.core.logging.SLF4JLogDelegateFactory"]

def mainClassName = 'com.hiddenswitch.spellsource.applications.Clustered'
apply plugin: 'com.github.johnrengelman.shadow'

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

shadowJar {
    mergeServiceFiles()
    archiveClassifier.set(null)
    configurations = [project.configurations.compile]
    zip64 true
}

artifacts {
    archives shadowJar
}

repositories {
    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        url 'https://jitpack.io'
    }
    jcenter()
}

dependencies {
    runtimeOnly group: 'org.codehaus.groovy', name: 'groovy', version: '3.0.0-rc-2'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-smile:2.9.9'
    compile 'com.fasterxml.jackson.module:jackson-module-afterburner:2.9.9'

    compile group: 'joda-time', name: 'joda-time', version: '2.9.9'
    compile group: 'com.lambdaworks', name: 'scrypt', version: '1.4.0'
    compile group: 'commons-validator', name: 'commons-validator', version: '1.4.1'
    compile group: 'io.vertx', name: 'vertx-core', version: '3.8.3'
    compile group: 'io.vertx', name: 'vertx-mongo-client', version: '3.8.3'
    compile group: 'io.vertx', name: 'vertx-web-client', version: '3.8.3'
    compile group: 'io.vertx', name: 'vertx-service-discovery', version: '3.8.3'
    compile group: 'io.vertx', name: 'vertx-web', version: '3.8.3'
    compile group: 'io.vertx', name: 'vertx-auth-common', version: '3.8.3'
    compile group: 'io.vertx', name: 'vertx-rx-java2', version: '3.8.3'
    compile group: 'io.vertx', name: 'vertx-mail-client', version: '3.8.3'
    compile group: 'io.vertx', name: 'vertx-dropwizard-metrics', version: '3.8.3'
    compile 'org.jetbrains:annotations-java5:15.0'
    compile 'io.jaegertracing:jaeger-client:0.35.5'
    compile 'io.netty:netty-transport-native-epoll:4.1.15.Final:linux-x86_64'
    compile 'io.netty:netty-transport-native-kqueue:4.1.15.Final:osx-x86_64'
    compile 'io.atomix:atomix:3.1.5'
    compile 'io.atomix:atomix-raft:3.1.5'
    compile 'io.atomix:atomix-primary-backup:3.1.5'
    compile group: 'javax.cache', name: 'cache-api', version: '1.1.1'
    compile group: 'net.sf.py4j', name: 'py4j', version: '0.10.8.1'
    compile group: 'ca.pjer', name: 'logback-awslogs-appender', version: '1.0.0'

    compile project(':cards')
    compile project(':client')
    compile project(':game')
    compile project(':vertx-sync')
    compile project(':game')

    testCompile project(':testutils')
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'io.vertx:vertx-core:3.8.0:tests'
    testCompile 'org.powermock:powermock-module-junit4:2.0.2'
    testCompile 'org.powermock:powermock-api-mockito2:2.0.2'
    testCompile group: 'io.vertx', name: 'vertx-unit', version: '3.8.3'
    testCompile group: 'io.github.robwin', name: 'assertj-swagger', version: '0.5.0'
    testCompile group: 'org.jboss.resteasy', name: 'resteasy-client', version: '3.1.0.Final'
    testCompile group: 'org.asynchttpclient', name: 'async-http-client', version: '2.4.9'
    testCompile group: 'com.neovisionaries', name: 'nv-websocket-client', version: '2.6'
}

test {
    setSystemProperties(debugSystemProperties)
    useJUnit()
    maxParallelForks = 1
    enableAssertions = false

    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }

    if (System.properties.containsKey("exclude-tests")) {
        exclude(System.properties.getProperty("exclude-tests").split(","))
    }
}

task(runDebug, dependsOn: 'classes', type: JavaExec) {
    setSystemProperties(debugSystemProperties)
    main = localMainClassName
    setJvmArgs(["-Xdebug", "-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5005"] as List<String>)
    classpath = sourceSets.main.runtimeClasspath
}

task(local, dependsOn: 'classes', type: JavaExec) {
    setSystemProperties(debugSystemProperties)
    main = localMainClassName
    classpath = sourceSets.main.runtimeClasspath
}

task(localWindows, dependsOn: 'classes', type: JavaExec) {
    doFirst {
        println "Local Windows Server Command"
        println ""
        println "Exiting batch scripts in Windows command line is very finicky. Never end execution by closing the console tab or Window. Instead, use ConEmu, and configure a hotkey to send the Break key. In ConEmu, you can do this by clicking the Hamburger menu in the upper right corner, choosing settings, and then configuring a break command as documented in  https://stackoverflow.com/questions/41074403/conemusend-sigint-to-running-application"
        println "MongoDB must be running before you start this command. If it's not installed, visit the Windows Development guide install for more details. You can install it with chocolatey using the command:"
        println "  choco install mongodb.install --version 3.6.0"
        println "Then, start mongodb with the commands:"
        println "  cd .\\Spellsource-Server"
        println "  md -Force .\\net\\.mongo\\db"
        println "  & \"C:\\Program Files\\MongoDB\\Server\\3.6\\bin\\mongod\" --dbpath .\\net\\.mongo\\db"
        println "When the server is ready, you'll see the text:"
        println "  Server is ready."
        println "Otherwise, you will see an error. Make sure mongod is running, as this is the most common mistake you will make."
    }
    setSystemProperties(debugSystemProperties)
    main = localMainClassName
    classpath = sourceSets.main.runtimeClasspath
}

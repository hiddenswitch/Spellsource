buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

def hazelcastJvmArgs = ["--add-modules", "java.se",
                        "--add-exports", "java.base/jdk.internal.ref=ALL-UNNAMED",
                        "--add-opens", "java.base/java.lang=ALL-UNNAMED",
                        "--add-opens", "java.base/java.nio=ALL-UNNAMED",
                        "--add-opens", "java.base/sun.nio.ch=ALL-UNNAMED",
                        "--add-opens", "java.management/sun.management=ALL-UNNAMED",
                        "--add-opens", "jdk.management/com.sun.management.internal=ALL-UNNAMED"]

def localMainClassName = 'com.hiddenswitch.spellsource.applications.LocalClustered'

def debugSystemProperties = ["spellsource.port"                        : "8080",
                             "spellsource.root.logger.level"           : "DEBUG",
                             "games.defaultNoActivityTimeout"          : "999999000",
                             "vertx.logger-delegate-factory-class-name": "io.vertx.core.logging.SLF4JLogDelegateFactory"]

apply plugin: 'application'
mainClassName = 'com.hiddenswitch.spellsource.applications.Clustered'
apply plugin: 'com.github.johnrengelman.shadow'

configurations.all {
    resolutionStrategy {
        force 'com.hazelcast:hazelcast:3.11'
        force 'org.apache.commons:commons-lang3:3.5'
        force 'com.fasterxml.jackson.core:jackson-core:2.9.7'
        force 'com.fasterxml.jackson.core:annotations:2.9.0'
        force 'com.fasterxml.jackson.core:databind:2.9.7'
    }
}

shadowJar {
    zip64 true
}

repositories {
    mavenCentral()
    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        url 'https://jitpack.io'
    }
    jcenter()
}

dependencies {
    compile group: 'com.hazelcast', name: 'hazelcast', version: '3.11'
    compile group: 'com.hazelcast', name: 'hazelcast-aws', version: '2.3'
    compile group: 'joda-time', name: 'joda-time', version: '2.9.9'
    compile group: 'com.lambdaworks', name: 'scrypt', version: '1.4.0'
    compile group: 'commons-validator', name: 'commons-validator', version: '1.4.1'
    compile group: 'io.vertx', name: 'vertx-core', version: '3.6.0'
    compile group: 'io.vertx', name: 'vertx-mongo-client', version: '3.6.0'
    compile group: 'io.vertx', name: 'vertx-web-client', version: '3.6.0'
    compile group: 'io.vertx', name: 'vertx-service-discovery', version: '3.6.0'
    compile group: 'io.vertx', name: 'vertx-web', version: '3.6.0'
    compile group: 'io.vertx', name: 'vertx-auth-common', version: '3.6.0'
    compile group: 'io.vertx', name: 'vertx-hazelcast', version: '3.6.0'
    compile group: 'io.vertx', name: 'vertx-rx-java2', version: '3.6.0'
    compile group: 'io.vertx', name: 'vertx-mail-client', version: '3.6.0'
    compile group: 'su.nlq', name: 'vertx-prometheus-metrics', version: '3.6.0'
    compile group: 'net.sf.py4j', name: 'py4j', version: '0.10.7'
    compile group: 'de.flapdoodle.embed', name: 'de.flapdoodle.embed.mongo', version: '2.0.3'
    compile group: 'org.reflections', name: 'reflections', version: '0.9.11'
    compile group: 'ca.pjer', name: 'logback-awslogs-appender', version: '1.0.0'
    
    /* Hazelcast Consul discovery support is pretty spotty
    compile group: 'org.bitsofinfo', name: 'hazelcast-consul-discovery-spi', version: '1.0-RC9'
    compile group: 'org.apache.cxf', name: 'cxf-rt-rs-client', version: '3.0.3'
    compile group: 'org.apache.cxf', name: 'cxf-rt-transports-http-hc', version: '3.0.3'
    */
    
    compile project(':game')
    compile project(':cards')
    compile project(':client')
    compile project(':vertx-sync')

    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile group: 'org.unitils', name: 'unitils-core', version: '3.4.3'
    testCompile group: 'io.vertx', name: 'vertx-unit', version: '3.6.0'
    testCompile group: 'io.github.robwin', name: 'assertj-swagger', version: '0.5.0'
    testCompile group: 'org.jboss.resteasy', name: 'resteasy-client', version: '3.1.0.Final'
    testCompile group: 'org.asynchttpclient', name: 'async-http-client', version: '2.4.9'
    testCompile group: 'com.neovisionaries', name: 'nv-websocket-client', version: '2.6'
}

test {
    setSystemProperties(debugSystemProperties)
    setJvmArgs(hazelcastJvmArgs)
    useJUnit()

    if (System.properties.containsKey("exclude-tests")) {
        exclude(System.properties.getProperty("exclude-tests").split(","))
    }

    testLogging {
        events "failed"
    }
}

task(runDebug, dependsOn: 'classes', type: JavaExec) {
    setSystemProperties(debugSystemProperties)
    main = localMainClassName
    setJvmArgs(hazelcastJvmArgs + ["-Xdebug", "-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5005"] as List<String>)
    classpath = sourceSets.main.runtimeClasspath
}

task(local, dependsOn: 'classes', type: JavaExec) {
    setSystemProperties(debugSystemProperties)
    main = localMainClassName
    setJvmArgs(hazelcastJvmArgs)
    classpath = sourceSets.main.runtimeClasspath
}

task(localWindows, dependsOn: 'classes', type: JavaExec) {
    doFirst {
        println "Local Windows Server Command"
        println ""
        println "Exiting batch scripts in Windows command line is very finicky. Never end execution by closing the console tab or Window. Instead, use ConEmu, and configure a hotkey to send the Break key. In ConEmu, you can do this by clicking the Hamburger menu in the upper right corner, choosing settings, and then configuring a break command as documented in  https://stackoverflow.com/questions/41074403/conemusend-sigint-to-running-application"
        println "MongoDB must be running before you start this command. If it's not installed, visit the Windows Development guide install for more details. You can install it with chocolatey using the command:"
        println "  choco install mongodb.install --version 3.6.0"
        println "Then, start mongodb with the commands:"
        println "  cd .\\Spellsource-Server"
        println "  md -Force .\\net\\.mongo\\db"
        println "  & \"C:\\Program Files\\MongoDB\\Server\\3.6\\bin\\mongod\" --dbpath .\\net\\.mongo\\db"
        println "When the server is ready, you'll see the text:"
        println "  Server is ready."
        println "Otherwise, you will see an error. Make sure mongod is running, as this is the most common mistake you will make."
    }
    setSystemProperties(debugSystemProperties)
    main = localMainClassName
    setJvmArgs(hazelcastJvmArgs)
    classpath = sourceSets.main.runtimeClasspath
}
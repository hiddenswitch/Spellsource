plugins {
    id 'com.github.johnrengelman.shadow' version "5.2.0"
    id 'application'
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'application'
apply from: '../gradle/instrument.gradle'

def localMainClassName = "com.hiddenswitch.spellsource.net.applications.LocalClustered"
def defaultMainClass = "com.hiddenswitch.spellsource.net.applications.Clustered"

def debugSystemProperties = ["spellsource.port"                        : System.getenv().getOrDefault("PORT", "8080"),
                             "spellsource.root.logger.level"           : "DEBUG",
                             "games.defaultNoActivityTimeout"          : "999999000",
                             "vertx.logger-delegate-factory-class-name": "io.vertx.core.logging.SLF4JLogDelegateFactory"]


mainClassName = localMainClassName

shadowJar {
    mergeServiceFiles()
    zip64 true
    manifest {
        attributes 'Main-Class': defaultMainClass
    }
}

artifacts {
    archives shadowJar
}

repositories {
    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        url 'https://jitpack.io'
    }
    jcenter()
}

configurations.runtime {
    exclude group: 'com.google.code.findbugs', module: 'jsr305'
    exclude group: 'javax.ws.rs', module: 'jsr311-api'
}

dependencies {
    implementation group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.1.1'
    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'

    runtimeOnly group: 'org.codehaus.groovy', name: 'groovy', version: '3.0.2'
    implementation('com.fasterxml.jackson.dataformat:jackson-dataformat-smile:2.10.2') {
        exclude group: 'jakarta.xml.bind', module: '*'
        exclude group: 'jakarta.activation', module: '*'
    }
    implementation('com.fasterxml.jackson.module:jackson-module-afterburner:2.10.2') {
        exclude group: 'jakarta.xml.bind', module: '*'
        exclude group: 'jakarta.activation', module: '*'
    }
    implementation 'io.github.classgraph:classgraph:4.8.59'
    implementation group: 'joda-time', name: 'joda-time', version: '2.10.2'
    implementation group: 'com.lambdaworks', name: 'scrypt', version: '1.4.0'
    implementation group: 'commons-validator', name: 'commons-validator', version: '1.6'
    api group: 'io.vertx', name: 'vertx-core', version: '3.9.1'
    implementation group: 'io.vertx', name: 'vertx-mongo-client', version: '3.9.1'
    implementation group: 'io.vertx', name: 'vertx-web-client', version: '3.9.1'
    implementation group: 'io.vertx', name: 'vertx-service-discovery', version: '3.9.1'
    implementation group: 'io.vertx', name: 'vertx-web', version: '3.9.1'
    implementation group: 'io.vertx', name: 'vertx-auth-common', version: '3.9.1'
    implementation group: 'io.vertx', name: 'vertx-rx-java2', version: '3.9.1'
    implementation group: 'io.vertx', name: 'vertx-mail-client', version: '3.9.1'
    implementation 'org.jetbrains:annotations:16.0.2'
    implementation 'io.jaegertracing:jaeger-client:1.2.0'
    implementation 'io.netty:netty-transport-native-epoll:4.1.43.Final:linux-x86_64'

    implementation('com.esotericsoftware:kryo:4.0.2') {
        exclude group: 'org.objenesis', module: '*'
    }

    implementation('io.atomix:atomix:3.1.5') {
        exclude group: 'org.objenesis', module: '*'
        exclude group: 'io.github.classgraph', module: 'classgraph'
    }
    implementation('io.atomix:atomix-raft:3.1.5') {
        exclude group: 'org.objenesis', module: '*'
        exclude group: 'io.github.classgraph', module: 'classgraph'
    }
    implementation('io.atomix:atomix-primary-backup:3.1.5') {
        exclude group: 'org.objenesis', module: '*'
        exclude group: 'io.github.classgraph', module: 'classgraph'
    }
    implementation group: 'javax.cache', name: 'cache-api', version: '1.1.1'
    implementation group: 'net.sf.py4j', name: 'py4j', version: '0.10.8.1'
    implementation group: 'ca.pjer', name: 'logback-awslogs-appender', version: '1.0.0'

    implementation project(':cards')
    implementation(project(':client')) {
        exclude group: 'jakarta.activation', module: 'jakarta.activation-api'
        exclude group: 'javax.ws.rs', module: 'jsr311-api'
        exclude group: 'jakarta.ws.rs', module: '*'
    }

    implementation project(':game')
    implementation project(':vertx-sync')
    implementation project(':game')

    testRuntimeOnly group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.1.1'
    testRuntimeOnly 'org.glassfish.jersey.core:jersey-common:2.+'

    testImplementation(platform("org.junit:junit-bom:5.6.0-M1"))
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine") {
        because 'allows JUnit 3 and JUnit 4 tests to run'
    }
    testRuntimeOnly(project(':client')) {
        exclude group: 'jakarta.activation', module: 'jakarta.activation-api'
        exclude group: 'javax.ws.rs', module: 'jsr311-api'
    }

    testRuntimeOnly("org.junit.platform:junit-platform-launcher") {
        because 'allows tests to run from IDEs that bundle older version of launcher'
    }

    testImplementation project(':testutils')
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation 'io.vertx:vertx-core:3.9.1:tests'
    testImplementation('org.powermock:powermock-module-junit4:2.0.2') {
        exclude group: 'org.objenesis', module: '*'
    }
    testImplementation('org.powermock:powermock-api-mockito2:2.0.2') {
        exclude group: 'org.objenesis', module: '*'
    }
    testImplementation group: 'io.vertx', name: 'vertx-unit', version: '3.9.1'
    testImplementation(group: 'org.jboss.resteasy', name: 'resteasy-client', version: '3.11.2.Final') {
        exclude group: 'javax.activation', module: 'activation'
        exclude group: 'org.jboss.spec.javax.annotation', module: '*'
        exclude group: 'org.jboss.spec.javax.ws.rs', module: '*'
        exclude group: 'org.jboss.spec.javax.xml.bind', module: '*'
    }
    testImplementation(group: 'org.asynchttpclient', name: 'async-http-client', version: '2.4.9') {
        exclude group: 'com.sun.activation', module: '*'
    }
    testImplementation group: 'com.neovisionaries', name: 'nv-websocket-client', version: '2.6'
}

run {
    description("Starts the Spellsource server")
    setSystemProperties(debugSystemProperties)
}

task(runDebug, type: JavaExec) {
    dependsOn classes
    description("Starts the Spellsource server attachable as a Remote debug target from IntelliJ")
    setSystemProperties(debugSystemProperties)
    setJvmArgs(["-Xdebug", "-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5005"] as List<String>)
    main = localMainClassName
}

test {
    description('Tests the server. Make sure mongod is running.')
    setSystemProperties(debugSystemProperties)
    useJUnitPlatform()
    maxParallelForks = 1
    enableAssertions = false

    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }

    if (System.properties.containsKey("exclude-tests")) {
        exclude(System.properties.getProperty("exclude-tests").split(","))
    }
}
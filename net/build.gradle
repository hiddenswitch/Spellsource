buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:0.5.4.RELEASE'
        classpath 'jp.classmethod.aws:gradle-aws-plugin:0.21'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'jp.classmethod.aws'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
    zip64 true
}

mainClassName = 'com.hiddenswitch.proto3.net.applications.Embedded'


aws {
    profileName = 'metastone_net'
    region = 'us-east-2'
}

configurations {
    quasar
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        url 'https://jitpack.io'
    }
}

dependencyManagement {
    imports {
        mavenBom 'com.amazonaws:aws-java-sdk-bom:1.11.52'
    }
}

dependencies {
    quasar files("lib/quasar-core-0.7.5-jdk8.jar")

    compile group: 'com.amazonaws', name: 'aws-java-sdk', version: '1.11.52'
    compile group: 'com.lambdaworks', name: 'scrypt', version: '1.4.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    compile group: 'commons-validator', name: 'commons-validator', version: '1.4.1'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
    compile group: 'io.vertx', name: 'vertx-core', version: '3.3.3'
    compile group: 'com.github.hiddenswitch', name: 'vertx-sync', version: '3.3.3.FA1'
    compile group: 'io.vertx', name: 'vertx-service-discovery', version: '3.3.3'
    compile group: 'io.vertx', name: 'vertx-mongo-client', version: '3.3.3'
    compile group: 'io.vertx', name: 'vertx-web', version: '3.3.3'
    compile group: 'io.vertx', name: 'vertx-auth-common', version: '3.3.3'
    compile group: 'de.flapdoodle.embed', name: 'de.flapdoodle.embed.mongo', version: '2.0.1-SNAPSHOT'

    compile project(':game')
    compile project(':cards')
    compile project(':client')

    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile group: 'org.unitils', name: 'unitils-core', version: '3.4.3'
    testCompile group: 'io.vertx', name: 'vertx-unit', version: '3.3.3'
    testCompile group: 'io.github.robwin', name: 'assertj-swagger', version: '0.5.0'
    testCompile group: 'org.jboss.resteasy', name: 'resteasy-client', version: '3.1.0.Final'
    testCompile group: 'javax.websocket', name: 'javax.websocket-client-api', version: '1.1'
    testCompile group: 'org.glassfish.tyrus', name: 'tyrus-container-grizzly-client', version: '1.13.1'
}

tasks.withType(JavaExec) {
    jvmArgs "-javaagent:${configurations.quasar.iterator().next()}"
}

test {
    jvmArgs "-javaagent:${configurations.quasar.iterator().next()}"
    useJUnit()
    testLogging {
        events "failed"
    }
}

task(runDebug, dependsOn: 'classes', type: JavaExec) {
    main = 'com.hiddenswitch.proto3.net.applications.Embedded'
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = ["-Xdebug", "-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5005", "-javaagent:${configurations.quasar.iterator().next()}"]
}

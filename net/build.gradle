buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
    zip64 true
}

mainClassName = 'com.hiddenswitch.spellsource.applications.Embedded'

configurations {
    quasar
}

repositories {
    mavenCentral()
    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        url 'https://jitpack.io'
    }
    jcenter()
}

dependencies {
    quasar files("lib/quasar-core-0.7.9-jdk8.jar")

    compile group: 'com.hazelcast', name: 'hazelcast-aws', version: '2.1.0'
    compile group: 'joda-time', name: 'joda-time', version: '2.9.9'
    compile group: 'com.lambdaworks', name: 'scrypt', version: '1.4.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    compile group: 'commons-validator', name: 'commons-validator', version: '1.4.1'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
    compile group: 'io.vertx', name: 'vertx-core', version: '3.5.0'
    compile group: 'com.github.hiddenswitch', name: 'vertx-sync', version: 'master-SNAPSHOT'
    compile group: 'io.vertx', name: 'vertx-service-discovery', version: '3.5.0'
    compile group: 'io.vertx', name: 'vertx-mongo-client', version: '3.5.0'
    compile group: 'io.vertx', name: 'vertx-web', version: '3.5.0'
    compile group: 'io.vertx', name: 'vertx-auth-common', version: '3.5.0'
    compile group: 'io.vertx', name: 'vertx-hazelcast', version: '3.5.0'
    compile group: 'com.github.flapdoodle-oss', name: 'de.flapdoodle.embed.mongo', version: 'e5232204c2'
    compile group: 'org.reflections', name: 'reflections', version: '0.9.11'

    compile project(':game')
    compile project(':cards')
    compile project(':client')

    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile group: 'org.unitils', name: 'unitils-core', version: '3.4.3'
    testCompile group: 'io.vertx', name: 'vertx-unit', version: '3.5.0'
    testCompile group: 'io.github.robwin', name: 'assertj-swagger', version: '0.5.0'
    testCompile group: 'org.jboss.resteasy', name: 'resteasy-client', version: '3.1.0.Final'
    testCompile group: 'javax.websocket', name: 'javax.websocket-client-api', version: '1.1'
    testCompile group: 'org.glassfish.tyrus', name: 'tyrus-container-grizzly-client', version: '1.13.1'
    testCompile group: 'org.apache.curator', name: 'curator-test', version: '3.3.0'
}

tasks.withType(JavaExec) {
    jvmArgs "-javaagent:${configurations.quasar.iterator().next()}=mb"
}

test {
    jvmArgs "-javaagent:${configurations.quasar.iterator().next()}=mb"
    systemProperty "spellsource.port", "8080"
    systemProperty "vertx.logger-delegate-factory-class-name", "io.vertx.core.logging.SLF4JLogDelegateFactory"
    useJUnit()
    testLogging {
        events "failed"
    }
}

task(runDebug, dependsOn: 'classes', type: JavaExec) {
    systemProperty "spellsource.port", "8080"
    main = 'com.hiddenswitch.spellsource.applications.Embedded'
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = ["-Xdebug", "-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5005", "-javaagent:${configurations.quasar.iterator().next()}=mb"]
}

task(local, dependsOn: 'classes', type: JavaExec) {
    systemProperty "spellsource.port", "8080"
    systemProperty "spellsource.root.logger.level", "DEBUG"
    main = 'com.hiddenswitch.spellsource.applications.Embedded'
    jvmArgs "-javaagent:${configurations.quasar.iterator().next()}=mb"
    classpath = sourceSets.main.runtimeClasspath
}

task(localWindows, dependsOn: 'classes', type: JavaExec) {
    doFirst {
        println "Local Windows Server Command"
        println ""
        println "Exiting batch scripts in Windows command line is very finicky. Never end execution by closing the console tab or Window. Instead, use ConEmu, and configure a hotkey to send the Break key. In ConEmu, you can do this by clicking the Hamburger menu in the upper right corner, choosing settings, and then configuring a break command as documented in  https://stackoverflow.com/questions/41074403/conemusend-sigint-to-running-application"
        println "MongoDB must be running before you start this command. If it's not installed, visit the Windows Development guide install for more details. You can install it with chocolatey using the command:"
        println "  choco install mongodb.install"
        println "Then, start mongodb with the commands:"
        println "  cd .\\Spellsource-Server"
        println "  md -Force .\\net\\.mongo\\db"
        println "  & \"C:\\Program Files\\MongoDB\\Server\\3.6\\bin\\mongod\" --dbpath .\\net\\.mongo\\db"
        println "When the server is ready, you'll see the text:"
        println "  Server is ready."
        println "Otherwise, you will see an error. Make sure mongod is running, as this is the most common mistake you will make."
    }
    systemProperty "spellsource.port", "8080"
    systemProperty "mongo.url", "mongodb://localhost:27017"
    main = 'com.hiddenswitch.spellsource.applications.Embedded'
    jvmArgs "-javaagent:${configurations.quasar.iterator().next()}=mb"
    classpath = sourceSets.main.runtimeClasspath
}

task(localCluster, dependsOn: 'classes', type: JavaExec) {
    doFirst {
        println "mongod must be running for this task."
        println "Automatic discovery of Hazelcast hosts is not configured."
    }

    systemProperty "spellsource.port", "8080"
    systemProperty "mongo.url", "mongodb://localhost:27017"
    main = 'com.hiddenswitch.spellsource.applications.TestClustered'
    jvmArgs "-javaagent:${configurations.quasar.iterator().next()}=mb"
    classpath = sourceSets.main.runtimeClasspath
}

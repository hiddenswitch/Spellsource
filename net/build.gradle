buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:0.5.4.RELEASE"
        classpath "jp.classmethod.aws:gradle-aws-plugin:0.21"
    }
}

task copyNativeDeps(type: Copy) {
    from(configurations.testCompile) {
        include "*.dylib"
        include "*.so"
        include "*.dll"
    }
    into 'build/libs'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'jp.classmethod.aws'
apply plugin: 'war'
apply plugin: 'jetty'

aws {
    profileName = 'metastone_net'
    region = 'us-east-1'
}


repositories {
    mavenCentral()
    maven {
        url 'http://dynamodb-local.s3-website-us-west-2.amazonaws.com/release'
    }
}

dependencyManagement {
    imports {
        mavenBom 'com.amazonaws:aws-java-sdk-bom:1.11.52'
    }
}

dependencies {
    compile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0.1'
    compile group: 'com.amazonaws', name: 'aws-java-sdk', version: '1.11.+'
    compile group: 'com.lambdaworks', name: 'scrypt', version: '1.4.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    compile group: 'commons-validator', name: 'commons-validator', version: '1.4.1'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version: '2.+'

    compile project(':server')
    compile project(':cards')

    testCompile project(':game')
    testCompile group: 'org.unitils', name: 'unitils-core', version: '3.4.3'
    testCompile group: 'org.testng', name: 'testng', version: '6.+'
    testCompile group: 'org.elasticmq', name: 'elasticmq-rest-sqs_2.11', version: '0.10.0'
    testCompile group: 'com.almworks.sqlite4java', name: 'sqlite4java', version: '1.0.392'
    testCompile group: 'com.almworks.sqlite4java', name: 'sqlite4java-win32-x86', version: '1.0.392'
    testCompile group: 'com.almworks.sqlite4java', name: 'sqlite4java-win32-x64', version: '1.0.392'
    testCompile group: 'com.almworks.sqlite4java', name: 'libsqlite4java-osx', version: '1.0.392'
    testCompile group: 'com.almworks.sqlite4java', name: 'libsqlite4java-linux-i386', version: '1.0.392'
    testCompile group: 'com.almworks.sqlite4java', name: 'libsqlite4java-linux-amd64', version: '1.0.392'
    testCompile group: 'com.amazonaws', name: 'DynamoDBLocal', version: '1.11.0.1'
    testCompile group: 'org.glassfish.jersey.test-framework', name: 'jersey-test-framework-core', version: '2+'
}

test {
    useTestNG()
    testLogging.showStandardStreams = true
    testLogging {
        events "standardError"
    }
}

test.dependsOn copyNativeDeps
test.doFirst {
    systemProperty "sqlite4java.library.path", 'build/libs'
}

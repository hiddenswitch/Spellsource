plugins {
    id "net.saliman.cobertura" version "2.5.4"
    id "com.github.johnrengelman.shadow" version "5.1.0"
}

apply plugin: "net.saliman.cobertura"
apply plugin: "com.github.johnrengelman.shadow"

group 'com.hiddenswitch'
version '0.8.0'

ext.vendor = "Parallel Universe Software Co."
ext.quasarJar = "${rootProject.projectDir}/quasar-core/build/libs/quasar-core-${version}.jar"
ext.java8 = JavaVersion.current() == JavaVersion.VERSION_1_8
ext.guavaVer = '26.0-jre'
ext.asmVer = project.ext.java8 ? '5.2' : '7.0'
ext.kotlinVer = '1.3.0'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.current()
    if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
        disableAutoTargetJvm()
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/releases" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    jcenter()
}

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()
    }
}

configurations {
    compileOnly
    provided
    timewarp
    // markdownDoclet
    shadowedJar.extendsFrom(runtime)
}

sourceSets {
    main {
        java {
            srcDir project.ext.java8 ? 'src/jdk8/java' : 'src/jdk11/java'
        }

        compileClasspath += configurations.compileOnly + configurations.provided
        runtimeClasspath += compileClasspath
    }

    test {
        java {
            srcDir 'src/test/java'
        }
        compileClasspath += configurations.compileOnly + configurations.provided
        runtimeClasspath += configurations.provided
    }

    jmh {
        java {
            srcDir 'src/jmh/java'
        }

        compileClasspath += main.output + test.compileClasspath + test.output
        runtimeClasspath += compileClasspath + test.runtimeClasspath
        compileClasspath += main.compileClasspath + main.output + test.compileClasspath + test.output
        runtimeClasspath += compileClasspath + main.runtimeClasspath + test.runtimeClasspath
    }

    classloadertest {
        java {
            srcDir 'src/classloadertest/java'
        }

        compileClasspath += main.output + test.compileClasspath
        runtimeClasspath += compileClasspath + test.runtimeClasspath
    }
}

compileJava {
    options.warnings = false;
}

dependencies {
    if (project.ext.java8) {
        compile files(fileTree(dir: 'baselib', includes: ['*.jar']))
        compile("io.dropwizard.metrics:metrics-core:3.2.3") {
            exclude group: 'org.slf4j', module: '*'
        }
    } else {
        compile("io.dropwizard.metrics:metrics-core:4.1.0-rc2") {
            exclude group: 'org.slf4j', module: '*'
        }
        compile "io.dropwizard.metrics:metrics-jmx:4.1.0-rc2"
    }

    provided "org.apache.ant:ant:1.10.5"
    compile 'com.google.errorprone:error_prone_annotations:2.3.2'
    compile("com.google.guava:guava:$guavaVer") {
        exclude group: 'com.google.errorprone', module: '*'
        exclude group: 'org.checkerframework', module: 'checker-qual'
    }
    timewarp 'co.paralleluniverse:timewarp:0.2.0-SNAPSHOT'
    testCompile 'co.paralleluniverse:timewarp:0.2.0-SNAPSHOT'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile('junit:junit:4.12') {
        exclude group: 'org.hamcrest', module: '*'
    }
    testCompile('com.google.truth:truth:0.42') {
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.google.errorprone', module: '*'
        exclude group: 'junit', module: 'junit'
    }

    testCompile('org.mockito:mockito-core:2.24.5') {
        exclude group: "org.ow2.asm", module: '*'
        exclude group: "net.bytebuddy", module: '*'
    }
    testRuntime "net.bytebuddy:byte-buddy:1.9.2" // for Mockito
    testCompile "org.ow2.asm:asm:$asmVer"

    jmhCompile 'org.openjdk.jmh:jmh-core:1.21'
    jmhCompile 'org.openjdk.jmh:jmh-generator-annprocess:1.21'

    annotationProcessor 'org.kohsuke.metainf-services:metainf-services:1.8'

    compile "org.hdrhistogram:HdrHistogram:2.1.10"
    compile("org.latencyutils:LatencyUtils:2.0.3") {
        exclude group: "org.hdrhistogram", module: '*'
    }
    compile "org.ow2.asm:asm:$asmVer"
    compile "org.ow2.asm:asm-analysis:$asmVer"
    compile "org.ow2.asm:asm-commons:$asmVer"
    compile "org.ow2.asm:asm-util:$asmVer"

    // The exclusions are needed to get asm 5.0.4 whilst Kryo still depends on 5.0.3
    compile "org.objenesis:objenesis:2.6"
    compile("com.esotericsoftware:kryo:4.0.2") {
        exclude group: "org.ow2.asm", module: '*'
        exclude group: "org.objenesis", module: '*'
    }
    compile("de.javakaffee:kryo-serializers:0.42") {
        exclude group: "com.esotericsoftware", module: '*'
        exclude group: "com.esotericsoftware.kryo", module: '*'
    }

    // this happens after we've filtered the dependency on ASM from core's module-info
    compileTestJava {
        if (!project.ext.java8) {
            options.compilerArgs += ["--add-modules", "org.objectweb.asm", "--add-reads", "co.paralleluniverse.quasar.core=org.objectweb.asm"]
        }
    }

    provided('junit:junit:4.12') {
        exclude group: 'org.hamcrest', module: '*'
    }
    // annotationProcessor 'com.google.auto.service:auto-service:1.0-rc1' // same as metainf-services
    // markdownDoclet "ch.raffael.pegdown-doclet:pegdown-doclet:1.1.1"
}

compileJava.dependsOn processResources
compileJava {
    doLast {
        scanAndInstrument(sourceSets.main, [configurations.provided, configurations.runtime])
    }
}

test {
    if (!project.ext.java8) {
        jvmArgs '--add-opens', 'java.base/java.lang=ALL-UNNAMED'
    }

    dependsOn = [jar, compileClassloadertestJava]
    useJUnit()

    systemProperty 'co.paralleluniverse.monitoring.flightRecorderLevel', '5'
    systemProperty 'co.paralleluniverse.globalFlightRecorder', 'true'
    systemProperty 'co.paralleluniverse.flightRecorderDumpFile', 'quasar.log'
    systemProperty 'co.paralleluniverse.monitoring.flightRecorderSize', '200000'

    jvmArgs "-javaagent:${project.ext.quasarJar}" // =vdmc (verbose, debug, allow monitors, check class)

    if (project.name != 'quasar-kotlin') { // TODO investigate why kotlin-plugin 1.1.2+ doesn't like it
        jvmArgs "-Xbootclasspath/a:${configurations.timewarp.singleFile}"
        if (System.getenv("TRAVIS") == 'true') {
            jvmArgs "-javaagent:${configurations.timewarp.singleFile}=3" // slow clock down x3
        }
    }

    jvmArgs "-Xmx1024m"

    beforeTest { desc ->
        logger.quiet("Running test: " + desc)
    }

    afterTest { desc, result ->
        if (result.resultType == TestResult.ResultType.FAILURE) {
            logger.quiet("Failed test ${desc.name} [${desc.className}] with exception: ${result.exception}")
            if (result.exception != null) {
                result.exception.printStackTrace()
            }
        }
    }

    doLast() {
        println "file://" + getReports().getHtml().getDestination() + "/index.html"
    }
}

['run', 'debug'].each { name ->
    project.task(name, type: JavaExec, dependsOn: [testClasses]) {
        classpath = sourceSets.main.runtimeClasspath
        if (project.hasProperty('mainClass')) {
            main = project.mainClass
        }
        if (project.hasProperty('args')) {
            args project.args.split('\\s+')
        }

        jvmArgs '-Xmx2048m'
        // jvmArgs "-Xbootclasspath/p:${System.getProperty("user.home")}/jsr166.jar"
        // systemProperty 'co.paralleluniverse.fibers.DefaultFiberPool.parallelism', '1'

        systemProperty "log4j.configurationFile", "${rootProject.projectDir}/log4j.xml"
        systemProperty "Log4jContextSelector", "org.apache.logging.log4j.core.async.AsyncLoggerContextSelector"
        jvmArgs "-javaagent:${project.ext.quasarJar}" // =vdmc (verbose, debug, allow monitors, check class)
    }
}

jar {
    manifest {
        attributes(
                "Implementation-Title": project.name,
                "Implementation-Version": version,
                "Implementation-Vendor": vendor,
                "Premain-Class": "co.paralleluniverse.fibers.instrument.JavaAgent",
                "Agent-Class": "co.paralleluniverse.fibers.instrument.JavaAgent",
                "Can-Retransform-Classes": "true",
                "Can-Redefine-Classes": "true",
                "Built-By": System.getProperty("user.name"),
        )
    }
}

tasks.withType(JavaExec) {
    classpath += sourceSets.test.runtimeClasspath + sourceSets.test.output + sourceSets.jmh.runtimeClasspath + sourceSets.jmh.output
}

configure(javadoc) {
    classpath += configurations.provided

    exclude 'module-info.java' // Temporary, as module path is not added

    options {
        addStringOption('Xdoclint:none', '-quiet')
    }
}

defaultTasks 'build'
/*
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}*/

task artifacts {
    group = "Help"
    description = "Displays the artifacts associated with each configuration of " + project
    doFirst {
        configurations.findAll().each { config ->
            println "${config}:"
            // config.allArtifacts.each { file -> println "--- " + file }
            config.allArtifacts.getFiles().each { file -> println "" + file }
            println ' '
        }
    }
}

/*
 * see:
 *   https://docs.gradle.org/current/userguide/java_plugin.html
 *   http://examples.javacodegeeks.com/core-java/gradle/gradle-sourcesets-example/
 *   http://stackoverflow.com/questions/15870662/gradle-create-a-new-jar-from-each-sourceset
 *   http://stackoverflow.com/questions/18190614/compiling-a-project-with-different-java-source-compatibility
 *   https://softnoise.wordpress.com/2014/09/07/gradle-sub-project-test-dependencies-in-multi-project-builds/
 */

apply plugin: 'com.github.johnrengelman.shadow'

ext.moduleName = 'co.paralleluniverse.quasar.core'

// remove default artifact
configurations.runtime.artifacts.with { archives ->
    archives.each {
        archives.remove(it)
    }
}

//[jar]*.enabled = false


configurations {
    archives.extendsFrom runtime
}

def scanAndInstrument(SourceSet sset, configs) {
    def cp = '' + sset.output.classesDirs.asPath + ':' + sset.output.resourcesDir + ':' + configs*.asPath.join(':')
    ant.taskdef(
            name: 'scanSuspendables', classname: 'co.paralleluniverse.fibers.instrument.SuspendablesScanner',
            classpath: cp)
    ant.scanSuspendables(
            auto: false,
            supersFile: "$sset.output.resourcesDir/META-INF/suspendable-supers",
            append: true) {
        fileset(dir: sset.output.classesDirs.asPath)
    }

    ant.taskdef(name: 'instrumentation', classname: 'co.paralleluniverse.fibers.instrument.InstrumentationTask',
            classpath: cp)
    ant.instrumentation(verbose: 'true', check: 'true', debug: 'true') {
        fileset(dir: sset.output.classesDirs.asPath) {
            exclude(name: 'co/paralleluniverse/fibers/instrument/*.class')
        }
    }
}